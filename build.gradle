plugins {
    id 'java'
    id "com.palantir.git-version" version "0.12.3"
    id 'org.asciidoctor.convert' version '2.4.0'
    id 'org.springframework.boot' version '2.6.0'
    id "com.gorylenko.gradle-git-properties" version "2.3.1"
    id 'com.adarshr.test-logger' version '3.1.0'
    id "com.ofg.uptodate" version "1.6.3"
    id "com.github.ben-manes.versions" version "0.39.0"
}

apply plugin: 'io.spring.dependency-management'

group = 'com.example'
version = gitVersion()
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

def snippetsDir = "build/generated-snippets"

dependencies {
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    asciidoctor "org.springframework.restdocs:spring-restdocs-asciidoctor"

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.retry:spring-retry'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: "junit", module: "junit"
    }
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-webtestclient'
    testImplementation 'org.springframework.security:spring-security-test'
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.8.1'
    // https://mvnrepository.com/artifact/org.springframework.cloud/spring-cloud-contract-wiremock
    testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '3.0.4'
}

springBoot {
    buildInfo()
}

bootRun {
    systemProperty "spring.output.ansi.enabled", "always"
}

test {
    useJUnitPlatform()
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    outputs.dir("build/resources/main")
    doLast {
        copy {
            from "build/asciidoc/html5"
            into "build/resources/main/static/docs"
            include "index.html"
        }
    }
    dependsOn test
}

//noinspection UnnecessaryQualifiedReference
task asciidoctorFast(type: org.asciidoctor.gradle.AsciidoctorTask) {
    inputs.dir snippetsDir
    dependsOn = []
    outputs.dir("build/resources/main")
    doLast {
        copy {
            from "build/asciidoc/html5"
            into "build/resources/main/static/docs"
            include "index.html"
        }
    }
}

bootJar.dependsOn asciidoctor

wrapper {
    gradleVersion = "5.4.1"
    distributionType = Wrapper.DistributionType.ALL
}
